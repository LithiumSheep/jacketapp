apply plugin: 'com.android.application'

@SuppressWarnings("GrMethodMayBeStatic")
def gitBranch() {
    def branch = ''
    def proc = 'git rev-parse --abbrev-ref HEAD'.execute()
    proc.in.eachLine { line -> branch = line }
    proc.err.eachLine { line -> println line }
    proc.waitFor()
    branch
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.2'
    defaultConfig {
        applicationId "com.lithiumsheep.jacketapp"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "0.1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix '.debug'
            versionNameSuffix ' DEBUG (' + gitBranch() + ')'
        }
        release {
            crunchPngs true
            minifyEnabled false
            //TODO: Bring in proguard rules
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

repositories {
    maven { url "https://jitpack.io" }
    google()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.12'

    implementation project(':weatherlib')

    // google-services
    compile 'com.google.android.gms:play-services-location:11.8.0'
    compile 'com.google.android.gms:play-services-places:11.8.0'

    // support libs
    implementation 'com.android.support:appcompat-v7:27.0.2'
    implementation 'com.android.support:design:27.0.2'
    implementation 'com.android.support:cardview-v7:27.0.2'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    // android arch
    implementation "android.arch.lifecycle:runtime:1.0.3"
    implementation "android.arch.lifecycle:extensions:1.0.0"    // LiveData and ViewModel

    // RxJava
    implementation 'io.reactivex.rxjava2:rxjava:2.1.7'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.0.0'

    // utils
    implementation 'com.jakewharton.timber:timber:4.6.0'
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.0.5'

    // custom views
    // implementation 'com.github.AhmadNemati:WindView:1.1.1'
    implementation 'com.github.arimorty:floatingsearchview:2.1.1'
    implementation 'com.squareup.picasso:picasso:2.5.2'

    // material/mikepenz
    implementation('com.mikepenz:materialdrawer:5.9.5@aar') { transitive = true }
    implementation "com.mikepenz:iconics-core:2.9.1@aar"
    implementation "com.mikepenz:iconics-views:2.9.2@aar"
    implementation 'com.mikepenz:community-material-typeface:1.9.32.2@aar'
    implementation 'com.mikepenz:weather-icons-typeface:2.0.10.3@aar'
}
